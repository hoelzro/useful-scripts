#!/usr/bin/env perl

use autodie;
no autodie 'close';

use strict;
use warnings;
use feature qw(say);
use experimental qw(signatures);

use POSIX qw(WEXITSTATUS);
use File::Temp qw(tempfile);

my ( $out, $out_filename ) = tempfile(DIR => '/var/lib/extra-metrics', UNLINK => 1);

say {$out} "# HELP pacman_package_updates Which packages are out-of-date on a system";
say {$out} "# TYPE pacman_package_updates gauge";

open my $pipe, '-|', 'checkupdates', '--nocolor';
while(<$pipe>) {
    chomp;
    if(/^(?<package>\S+)\s+(?<version_old>\S+)\s*->\s*(?<version_new>\S+)/) {
        my ( $package, $version_old, $version_new ) = @+{qw/package version_old version_new/};
        say {$out} qq[pacman_package_updates{package="$package", version_old="$version_old", version_new="$version_new"} 1];
    } else {
        die "Unable to parse line '$_'";
    }
}
close $pipe;
my $checkupdates_exit_code = WEXITSTATUS($?);
close $out;

# checkupdates exits with 2 if there are no updates available
if($checkupdates_exit_code == 2) {
    $checkupdates_exit_code = 0;
}

if($checkupdates_exit_code == 0) {
    chmod 0444, $out_filename;
    rename $out_filename, '/var/lib/extra-metrics/pacman.prom';
}

exit $checkupdates_exit_code;
